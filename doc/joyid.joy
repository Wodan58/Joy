(*
    This program determines the version of the Joy language processor.
    The versions to be detected are: Moy, Foy, joy1, Joy, the legacy version,
    joy0, 42minjoy.

    Detection is based on features that are part of the language
    implementation that cannot be changed.
*)

(*
    42minjoy accepts numbers in an alternative radix. The default is 2.
*)
[&10 2 = [[] [['4'2'm'i'n'j'o'y' 'i's' 'e'x'e'c'u't'i'n'g'.'\010]
[putch] step]] of i].

(*
    The original joy0 has nullval as builtin.
*)
[0 nullval =] ["The original joy0 is executing.\n" [putch] step quit]
[] ifte.

(*
    Joy clears the stack after a definition. This is caused by the copying
    garbage collector and the desire not to recycle definitions.
*)
123.
DEFINE a == 456.
[stack size] [] ["The Joy with NOBDW is executing.\n" putchars quit] ifte.

(*
    Foy gets symbols as strings.
*)
get dup.
EXIT
["The Foy is executing.\n" putchars quit] [] ifstring.

(*
    Moy lets of/at accept a character as index, just like 42minjoy.
*)
'\001 [[] ["The Moy with yacc parser executing.\n" putchars quit]] of i.

(*
    The legacy version does not allow "true" to be redefined.
*)
DEFINE true == 0.
true [] ["The legacy version is executing.\n" putchars quit] [] ifte.

(*
    No other implementation is known, so it must be joy1 compiled with BDW.
*)
"The joy1 with BDW is executing.\n" putchars pop pop.
